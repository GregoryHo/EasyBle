apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

Properties properties = new Properties()
File localProperties = project.rootProject.file("local.properties")
if (localProperties.exists()) {
  properties.load(localProperties.newDataInputStream())
}

// Home page url
def siteUrl = 'https://github.com/GregoryHo/EasyBle'
// git repository url
def gitUrl = 'https://github.com/GregoryHo/EasyBle.git'
// Maven Group ID for the artifactï¼Œ
group = "greg.ns.library"
install {
  repositories.mavenInstaller {
    // This generates POM.xml with proper parameters
    pom {
      project {
        packaging 'aar'
        // Add your description here
        name 'A easy ble module'
        url siteUrl
        // Set your license
        licenses {
          license {
            name 'MIT License'
            url 'https://opensource.org/licenses/MIT'
          }
        }
        developers {
          developer {
            id 'Gregory'
            name 'Gregory'
            email 'greghojob@gmail.com'
          }
        }
        scm {
          connection gitUrl
          developerConnection gitUrl
          url siteUrl
        }
      }
    }
  }
}

task sourcesJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier = 'sources'
}

task javadoc(type: Javadoc) {
  options.encoding = 'UTF-8'
  source = android.sourceSets.main.java.srcDirs
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

javadoc {
  options {
    encoding 'UTF-8'
    charSet 'UTF-8'
    author true
    version true
  }
}

artifacts {
  archives javadocJar
  archives sourcesJar
}

bintray {
  user = properties.getProperty("bintray.user")
  key = properties.getProperty("bintray.apikey")
  configurations = ['archives']
  pkg {
    repo = "maven"
    name = "easy-ble"
    websiteUrl = siteUrl
    vcsUrl = gitUrl
    licenses = ["MIT"]
    publish = true
  }
}